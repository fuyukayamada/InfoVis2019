<html>
    <head>
    <title>W09: Task03</title>
    </head>

<!-- Display divided -->
    <div id="display" style="height: 80%; float: left;"></div>


<!-- シェーダー一覧 -->
<script type="x-shader/x-vertex" id="Lambertian.vert">
  // シェーダー１
        varying vec3 point_color;
        varying vec4 point_position;
        varying vec3 normal_vector;
        uniform vec3 light_position;
        uniform vec3 uColor;
        // LambertianReflection function
        vec3 LambertianReflection( vec3 C, vec3 L, vec3 N ) {
            float ka = 0.4;
            float kd = 0.6;
            
            float dd = max(dot( N, L ), 0.0 );
            float Ia = ka;
            float Id = kd * dd;
            return C * ( Ia + Id );
        }
        void main() {
            point_position = modelViewMatrix * vec4( position, 1.0 ); 
            normal_vector = normalMatrix * normal;
            vec3 C = color;
            vec3 L = normalize( light_position - point_position.xyz ); 
            vec3 N = normalize( normal_vector );
            point_color = LambertianReflection( C, L, N );
            gl_Position = projectionMatrix * point_position;
        } 
</script>    


<script type="x-shader/x-vertex" id="Phong.vert">
// シェーダー２
        varying vec3 point_color;
        varying vec4 point_position;
        varying vec3 normal_vector;
        uniform vec3 light_position;
        // PhongReflection function here
        vec3 PhongReflection( vec3 C, vec3 L, vec3 N, vec3 V ) {
            float ka = 0.3;
            float kd = 0.5;
            float ks = 0.8;
            float n = 50.0;
            
            vec3 R = reflect( -L, N );
            float dd = max( dot( N, L ), 0.0 );
            float ds = pow( max( dot( R, V ), 0.0 ), n ); 
            if ( dd <= 0.0 ) { ds = 0.0; }
            
            float Ia = ka;
            float Id = kd * dd;
            float Is = ks * ds;
            return C * ( Ia + Id + Is );
        }
        
        void main() {
            point_position = modelViewMatrix * vec4( position, 1.0 ); 
            normal_vector = normalMatrix * normal;
            vec3 C = color;
            vec3 L = normalize( light_position - point_position.xyz ); 
            vec3 N = normalize( normal_vector );
            vec3 V = normalize(cameraPosition - point_position.xyz);
            point_color = PhongReflection( C, L, N, V );
            gl_Position = projectionMatrix * point_position;
        } 
</script>


<script type="x-shader/x-vertex" id="Blinn-Phong.vert">
    // シェーダー３
      varying vec3 point_color;
      varying vec4 point_position;
      varying vec3 normal_vector;
      uniform vec3 light_position;
    // LambertianReflection function here
    
    vec3 PhongReflection( vec3 C, vec3 L, vec3 N , vec3 V)
      {
        float ka = 0.3;
        float kd = 0.5;
        float ks = 0.8;
        float n = 50.0;
        vec3 H   = normalize(L + V);
    
    
        float dd = max( dot( N, L ), 0.0 );
        float ds = pow( max( dot( H, N ), 0.0 ), n );
        if ( dd <= 0.0 ) { ds = 0.0; }
        float Ia = ka;
        float Id = kd * dd;
        float Is = ks * ds;
    
        return C * ( Ia + Id + Is );
      }
    void main()
      {
        point_position = modelViewMatrix * vec4( position, 1.0 );
        normal_vector = normalMatrix * normal;
        vec3 C = color;
        vec3 L = normalize( light_position - point_position.xyz );
        vec3 N = normalize( normal_vector );
        vec3 V = normalize( -(vec3(point_position)/point_position.w));
        point_color = PhongReflection( C, L, N ,V);
        gl_Position = projectionMatrix * point_position;
    }
</script>


<script type="x-shader/x-vertex" id="Cook-Torrance.vert">
    // シェーダー４
        varying vec3 point_color;
        varying vec4 point_position;
        varying vec3 normal_vector;
        uniform vec3 light_position;
        // CookTorranceReflection function here
        vec3 CookTorranceReflection( vec3 C, vec3 L, vec3 N, vec3 V ) {
            float ka = 0.3;
            float kd = 0.5;
            float ks = 0.5;
            float n = 10.0;
            float refrac = 100.0;
            vec3 H = normalize(L + V);
            
            float NV = dot( N, V );
            float NH = dot( N, H );
            float VH = dot( V, H );
            float NL = dot( N, L );
            
            float dd = max( NL, 0.0 );
            float F_g = sqrt(refrac * refrac + VH * VH - 1.0);
            
            float D = exp((pow(NH,2.0)-1.0)/pow(n*NH,2.0))/(4.0*pow(n,2.0)*pow(NH,4.0));
            float G = min(1.0, min(2.0*NH*NV/VH, 2.0*NH*NL/VH) );
            float F = (1.0+(pow(VH*(F_g+VH)-1.0,2.0)/pow(VH*(F_g-VH)-1.0,2.0)))*pow(F_g-VH,2.0)/pow(F_g+VH,2.0);
            
            //DGF/dot(N,V)
            float ds = D * G * F / NV; 
            if ( dd <= 0.0 ) { ds = 0.0; }
            
            float Ia = ka;
            float Id = kd * dd;
            float Is = ks * ds;
            return C * ( Ia + Id + Is );
        }
        
        void main() {
            point_position = modelViewMatrix * vec4( position, 1.0 ); 
            normal_vector = normalMatrix * normal;
            vec3 C = color;
            vec3 L = normalize( light_position - point_position.xyz ); 
            vec3 N = normalize( normal_vector );
            vec3 V = normalize(cameraPosition - point_position.xyz);
            point_color = CookTorranceReflection( C, L, N, V );
            gl_Position = projectionMatrix * point_position;
        } 
</script>


<script type="x-shader/x-vertex" id="Toon.vert">
    // シェーダー５
          varying vec3 point_color;
          varying vec4 point_position;
          varying vec3 normal_vector;
          uniform vec3 light_position;
          uniform vec3 camera_position;
          vec3 ToonReflection(vec3 C,vec3 L,vec3 N,vec3 V){
            float ka = 0.3;
            float kd = 0.5;
            float ks = 0.8;
            float n = 20.0;
            vec3 H = normalize(L+V);
            vec3 R = reflect(-L,N);
            float dd = max(dot(N,L),0.0);
            float ds = pow(max(dot(R,V),0.0),n);
            if(dd <= 0.0){
              ds=0.0;
            }
            float Ia = ka;
            float Id = kd * dd;
            float Is = ks * ds;
            const float ref1 = 0.1;
            const float ref2 = 0.3;
            const float ref3 = 0.6;
            const float ref4 = 1.0;
            float df = max(0.0,dot(N,L));
            //classify the df value
            if(df<ref1) df = 0.0;
            else if(df<ref2) df = ref2;
            else if(df<ref3) df = ref3;
            else df = ref4;
            float sf = max(0.0,dot(N,H));
            sf = pow(sf,n);
            return C*(Ia + df * Id + sf * Is);
          }
          void main() {
            point_position = modelViewMatrix * vec4(position,1.0);
            normal_vector = normalMatrix*normal;
            vec3 C = color;
            vec3 L = normalize(light_position - point_position.xyz);
            vec3 N = normalize(normal_vector);
            vec3 V = normalize(camera_position - point_position.xyz);
            point_color = ToonReflection(C,L,N,V);
            gl_Position = projectionMatrix * point_position;
          }
</script>


<script type="x-shader/x-fragment" id="Shader.frag">
  varying vec3 point_color;
  void main()
  {
  gl_FragColor = vec4( point_color, 1.0 );
  }
</script>






    <body style="margin:0">
    <script src="three.min.js"></script>
    <script src="TrackballControls.js"></script>
    <script src="https://threejs.org/examples/js/math/Lut.js"></script>
    <script src="https://naohisas.github.io/KVS.js/Build/KVS.min.js"></script>
    <script src="https://naohisas.github.io/KVS.js/Build/KVS2THREE.min.js"></script>
    <script src="https://naohisas.github.io/KVS.js/Source/KVSLobsterData.js"></script>
    <script src="Bounds.js"></script>
    <script src="Isosurfaces.js"></script>
    <script src="FinalTask.js"></script>
    <script src="dat.gui/dat.gui.js"></script>
    <script src="dat.gui/dat.gui.min.js"></script>

	<script>
	 main();
	</script>
    </body>
</html>
